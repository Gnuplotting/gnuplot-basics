#!/usr/bin/gnuplot
#
# Plotting data about battery power (see battery.dat)
#
# AUTHOR: Hagen Wierstorf

reset

# png
set terminal pngcairo size 350,262 enhanced font 'Verdana,10'
set output 'battery_data.png'

set border linewidth 1.5
# Set color of linestyle 1 to blue (#0060ad)
set style line 1 linecolor rgb '#0060ad' linetype 1 linewidth 2 pointtype 7

unset key
set xlabel 'Resistance (Î©)'
set ylabel 'Power (mW)'
set format y '%.0s'
set tics scale 0.75

plot [-2:52][0:0.12] 'battery.dat' using 1:2:4 with yerrorbars linestyle 1, \
                     '' with lines linestyle 1
